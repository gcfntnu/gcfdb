#-*- mode:snakemake -*-
"""Converters between common fileformats
"""



rule convert_gtf2gene_csv:
    input:
        join(REF_DIR, 'genes', 'genes.gtf')
    params:
        script = srcdir('scripts/gtf2genes.py')
    output:
        join(REF_DIR, 'genes', 'genes.tsv')
    singularity:
        'docker://quay.io/biocontainers/pyensembl:1.8.4--py_0'
    shell:
        'python {params.script} {input} > {output}'
        
rule convert_gtf2transcript_csv:
    input:
        join(REF_DIR, 'genes', 'genes.gtf')
    params:
        script = srcdir('scripts/gtf2tx.py')
    output:
        join(REF_DIR, 'genes', 'transcripts.tsv')
    singularity:
        'docker://quay.io/biocontainers/pyensembl:1.8.4--py_0'
    shell:
        'python {params.script} {input} > {output}'    

rule convert_gtf2txg:
    input:
        rules.convert_gtf2gene_csv.output
    output:
        join(REF_DIR, 'genes', 'tx2gene.tsv')
    shell:
        'cut -f1,10 {input} > {output}'
        
rule convert_fasta2sequence_dict:
    input:
        join(REF_DIR, 'fasta', 'genome.fa')
    output:
        join(REF_DIR, 'fasta', 'genome.dict')
    singularity:
        'docker://broadinstitute/picard:2.21.1'    
    shell:
        'java -jar /usr/picard/picard.jar CreateSequenceDictionary -Xms2g -Xmx2g R={input} O={output}'
        
rule convert_gtf2bed12:
    input:
        join(REF_DIR, 'genes', 'genes.gtf')
    output:
        join(REF_DIR, 'genes', 'genes.bed12')
    params:
        tmpfile = join(TMPDIR, 'gene.genepred')
    singularity:
        'docker://gcfntnu/ucsc-scripts'
    shell:
        """
        gtfToGenePred -genePredExt -geneNameAsName2 {input} {params}
        genePredToBed {params} {output}
        rm {params.tmpfile}        
        """
        
rule convert_gtf2refflat:
    input:
        join(REF_DIR, 'genes', 'genes.gtf')
    output:
         join(REF_DIR, 'genes', 'genes.refflat.gz')
    params:
        tmpfile = join(TMPDIR, 'refflat.tmp')
    singularity:
        'docker://gcfntnu/ucsc-scripts'
    shell:
        """
        gtfToGenePred -genePredExt -geneNameAsName2 {input} {params}
        paste <(cut -f 12 {params}) <(cut -f 1-10 {params}) > refFlat.txt
        rm {params.tmpfile}
        gzip refFlat.txt
        mv refFlat.txt.gz {output}
        """
        
rule convert_gtf_transcriptome_gffread:
    input:
        gtf = join(REF_DIR, 'genes', 'genes.gtf'),
        genome = join(REF_DIR, 'fasta', 'genome.fa')
    output:
        join(REF_DIR, 'fasta', 'gtf.gffread.transcripts.fa')
    singularity:
        'docker://quay.io/biocontainers/gffread:0.11.4--hdbcaa40_0'
    shell:
        'gffread -w {output} -G -E -g {input.genome} {input.gtf}'
        
rule convert_gtf_transcriptome_rsem:
    input:
        gtf = join(REF_DIR, 'genes', 'genes.gtf'),
        genome = join(REF_DIR, 'fasta', 'genome.fa')
    params:
        base = join(REF_DIR, 'fasta', 'gtf.rsem')
    singularity:
        'docker://quay.io/biocontainers/rsem:1.3.2--pl526ha52163a_1'
    output:
        join(REF_DIR, 'fasta', 'gtf.rsem.transcripts.fa')
    shell:
        'rsem-prepare-reference --gtf {input.gtf} {input.genome} {params.base}'
