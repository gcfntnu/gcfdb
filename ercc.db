#-*-mode:snakemake-*-
"""
adding a biotype annotation to gtf
[optional]: renaming ERCC probes features to `gene` to get the exons counted in a gene count pipeline
"""
from os.path import join

ERCC_DIR = join(EXT_DIR, 'ERCC')

rule ercc_files:
    params:
        url = 'https://tools.thermofisher.com/content/sfs/manuals/ERCC92.zip',
        date = datetime.now().strftime("%d-%m-%Y")
    output:
        ercc_gtf = join(ERCC_DIR, 'ERCC92.gtf'),
        ercc_fasta = join(ERCC_DIR, 'ERCC92.fa')
    log:
       join(ERCC_DIR, 'ERCC92.log') 
    shell:
        """
        wget {params.url}
        unzip ERCC92.zip
        mv ERCC92.gtf {output.ercc_gtf}
        mv ERCC92.fa {output.ercc_fasta}
        rm ERCC92.zip
        echo "ERCC,NA,{params.url},{params.date}" > {log}
        """

rule ercc_gtf:
    input:
        rules.ercc_files.output.ercc_gtf
    output:
        join(ERCC_DIR, 'ERCC92.fix.gtf')
    params:
        script = srcdir('scripts/fix_ercc_gtf.py')
    shell:
        'python {params.script} {input} > {output}'

rule ercc_gtf_gene:
    input:
        rules.ercc_files.output.ercc_gtf
    output:
        join(ERCC_DIR, 'ERCC92.gene.gtf')
    params:
        script = srcdir('scripts/fix_ercc_gtf.py')
    shell:
        'python {params.script} {input} --exon2gene > {output}'    

rule ercc_all:
    input:
        join(ERCC_DIR, 'ERCC92.fa'),
        join(ERCC_DIR, 'ERCC92.fix.gtf'),
        join(ERCC_DIR, 'ERCC92.gene.gtf')
